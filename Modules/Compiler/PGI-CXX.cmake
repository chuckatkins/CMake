include(Compiler/PGI)
__compiler_pgi(CXX)
string(APPEND CMAKE_CXX_FLAGS_MINSIZEREL_INIT " -DNDEBUG")
string(APPEND CMAKE_CXX_FLAGS_RELEASE_INIT " -DNDEBUG")

set(CMAKE_CXX98_STANDARD_COMPILE_OPTION  -A)
set(CMAKE_CXX98_EXTENSION_COMPILE_OPTION --gnu_extensions)
if(NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS 13.10)
  set(CMAKE_CXX11_STANDARD_COMPILE_OPTION  --c++11 -A)
  set(CMAKE_CXX11_EXTENSION_COMPILE_OPTION --c++11 --gnu_extensions)
  if(NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS 15.7)
    set(CMAKE_CXX14_STANDARD_COMPILE_OPTION  --c++14 -A)
    set(CMAKE_CXX14_EXTENSION_COMPILE_OPTION --c++14 --gnu_extensions)
  endif()
endif()

if(NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS 13.10)
  if (NOT CMAKE_CXX_COMPILER_FORCED)
    if (NOT CMAKE_CXX_STANDARD_COMPUTED_DEFAULT)
      message(FATAL_ERROR "CMAKE_CXX_STANDARD_COMPUTED_DEFAULT should be set for ${CMAKE_CXX_COMPILER_ID} (${CMAKE_CXX_COMPILER}) version ${CMAKE_CXX_COMPILER_VERSION}")
    endif()
    set(CMAKE_CXX_STANDARD_DEFAULT ${CMAKE_CXX_STANDARD_COMPUTED_DEFAULT})
  elseif(NOT DEFINED CMAKE_CXX_STANDARD_DEFAULT)
    # Compiler id was forced so just guess the default standard level.
    set(CMAKE_CXX_STANDARD_DEFAULT 98)
  endif()
endif()

macro(cmake_record_cxx_compile_features)
  foreach(STD IN ITEMS 98 11 14)
    record_compiler_features(CXX "${CMAKE_CXX${STD}_STANDARD_COMPILE_OPTION}"
      CMAKE_CXX${STD}_COMPILE_FEATURES)
  endforeach()
endmacro()

